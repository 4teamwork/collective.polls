*** Settings ***

Resource  plone.txt

Suite Setup  Setup

*** Variables ***

${poll-link-placeholder}  a#collective-polls-poll
${title-field-placeholder}  input#form-widgets-IBasic-title
${description-field-placeholder}  textarea#form-widgets-IBasic-description
${option-placeholder}  textarea.task-description

*** Test cases ***

Test CRUD
    Goto Homepage
    Create, Update, Delete Poll  Test poll
    Add Poll in Private Folder  Test Folder  Test Poll
    Add Poll in Public Folder  Test Folder  Test Poll

*** Keywords ***

Setup
    Log In  admin  admin

Add Option
    [Arguments]  ${option}

    Input Text  css=${option-placeholder}  ${option}
    Click Button  Add  Don't Wait
    Wait Until Page Contains  ${option}

Remove Option
    [Documentation]  Should remove the named option; this is not possible with
    ...              current implementation of the JavaScript library, so now
    ...              we are removing the first option only
    [Arguments]  ${option}

    Click Link  Delete task  Don't Wait
    Wait Until Keyword Succeeds  1  5  Page Should Not Contain  ${option}

Add Poll
    [Arguments]  ${title}

    Open Add New Menu
    Click Link  css=${poll-link-placeholder}
    Page Should Contain  Add Poll
    Input Text  css=${title-field-placeholder}  ${title}
    Add Option  Maybe
    Add Option  Yes
    Add Option  No
    Click Button  Save
    Page Should Contain  Item created

Edit Poll
    [Arguments]  ${title}

    Click Link  ${title}
    Click Link  Edit
    Page Should Contain  Edit Poll
    Remove Option  Maybe
    Click Button  Save
    Page Should Contain  Changes saved

Delete Poll
    [Arguments]  ${title}

    Click Link  ${title}
    Click Delete Action
    Wait Until Page Contains  Do you really want to delete this item?
    Click Button  Delete

Create, Update, Delete Poll
    [Arguments]  ${title}

    Add Poll  ${title}
    Edit Poll  ${title}
    Delete Poll  ${title}

Issue X
    [Documentation]  This tests demonstrates issue X
    [Arguments]  ${title}

    Click Link  ${title}
    Click Link  Edit
    Page Should Contain  Allow anonymous
    Click Button  Cancel

Create Folder
    [arguments]  ${title}

    Open Add New Menu
    Click Link  Folder
    Input Text  title  ${title}
    Click Button  Save
    Page Should Contain  Changes saved.

Open Change Workflow Menu
    Open Menu  plone-contentmenu-workflow

Add Poll Check Warning
    [Arguments]  ${title}

    Open Add New Menu
    Click Link  css=${poll-link-placeholder}
    Page Should Contain  Add Poll
    Input Text  css=${title-field-placeholder}  ${title}
    Add Option  Maybe
    Add Option  Yes
    Add Option  No
    Page Should Contain  Allow not logged in users to vote. The parent folder of this poll should be published before opeining the poll for this field to take effect
    Click Button  Save
    Page Should Contain  Item created
    


Add Poll in Private Folder
    [arguments]  ${title}  ${titlepoll}
    Create Folder  ${title}
    Add Poll Check Warning  ${titlepoll}
    Open Change Workflow Menu
    Click Link  Open
    Page Should Contain  Anonymous user won't be able to vote, you forgot to publish the parent folder, you must sent back the poll to private state, publish the parent folder and open the poll again
    Goto Homepage

Add Poll in Public Folder
    [arguments]  ${title}  ${titlepoll}
    Create Folder  ${title}
    Click Link  Publish
    Open Change Workflow Menu
    Add Poll Check Warning  ${titlepoll}
    Open Change Workflow Menu
    Click Link  Open
    Page Should Not Contain  Anonymous user won't be able to vote, you forgot to publish the parent folder, you must sent back the poll to private state, publish the parent folder and open the poll again
    Goto Homepage